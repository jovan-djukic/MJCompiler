package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.logging.log4j.Logger;
import rs.ac.bg.etf.pp1.utilities.MyLoggerFactory;
import java.util.*;
import rs.etf.pp1.symboltable.concepts.*;
import rs.etf.pp1.symboltable.structure.*;
import rs.ac.bg.etf.utilities.*;


parser code {:
	
	private Logger logger = MyLoggerFactory.getLogger(getClass());

	public Logger getLogger() {
		return logger;	
	}    

	
   	//redefined error functions  
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      report_error(message, info);
      done_parsing();
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("Syntax erorr: ", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatal error, cannot continue parsing, symbol: ", cur_token);
    }

    public void report_error(String message, Object info) {
        if (info != null && info instanceof Symbol) {
			Symbol currentSymbol = (Symbol) info;
			message += "at line: " + (currentSymbol.left + 1) + ", at column: " + (currentSymbol.right + 1);
			logger.info(message + ", current symbol: " + info.toString());
		} else {
			logger.info(message);
		}
		errorFound = true;
    }

    private boolean errorFound = false;

    public void report_semantic_error(String message, int lineNumber) {
    	report_error("Semantic error on line: " + lineNumber + ", " + message, null);
    	errorFound = true;
    }

:}


scan with {:
	Symbol symbol = this.getScanner().next_token();
	if (symbol != null && symbol.value != null) 
		logger.debug(symbol.toString() + " " + symbol.value.toString());
	return symbol;
:}

init with {:
	SymbolTable.init(); // Universe scope
:}

action code {:
	//functions defined for counts
	private int globalVariableCount = 0;

	public int getGlobalVariableCount() {
		return globalVariableCount;
	}

	private int currentMethodVariableCount = 0;

	public void incrementCurrentMethodVariableCount() {
		currentMethodVariableCount++;
	}

	public void invalidateCurrentMethodVariableCount() {
		currentMethodVariableCount = 0;
	}
	
	public void incrementGlobalVariableCount() {
		globalVariableCount++;
	}

	private int mainVariableCount = 0;

	public int getMainVariableCount() {
		return mainVariableCount;
	}

	public void incrementMainVariableCount() {
		mainVariableCount += currentMethodVariableCount;
		currentMethodVariableCount = 0;
	}

	private static String MAIN = "main";
	
	public boolean isMain(String identifier) {
		return MAIN.equals(identifier);
	}

	private int constantCount = 0;

	public int getConstantCount() {
		return constantCount;
	}

	public void incrementConstantCount() {
		constantCount++;
	}

	private int globalArrayCount = 0;
	private int currentArrayCount = 0;

	public int getGlobalArrayCount() {
		return globalArrayCount;
	}

	public void incrementGlobalArrayCount() {
		globalArrayCount += currentArrayCount;
		currentArrayCount = 0;	
	}

	public void incrementCurrentArrayCount() {
		currentArrayCount++;
	}

	public void invalidateCurrentArrayCount() {
		currentArrayCount = 0;	
	}

	private int staticMethodCount = 0;
	private int nonStaticMethodCount = 0;
	private int currentNonStaticMethodCount = 0;

	public int getStaticMethodCount() { 
		return staticMethodCount;
	}

	public int getNonStaticMethodCount() { 
		return nonStaticMethodCount;
	}

	public int getGlobalMethodCount() {
		return currentNonStaticMethodCount;
	}

	public void incrementCurrentNonStaticMethodCount() {
		currentNonStaticMethodCount++;
	}

	public void incrementStaticMethodCount() {
		staticMethodCount++;
	}

	public void incrementNonStaticMethodCount() {
		nonStaticMethodCount += currentNonStaticMethodCount;
		currentNonStaticMethodCount = 0;
	}

	private int codeBlockCount = 0;
	private int currentMethodCallCount = 0;

	public int getCodeBlockCount() {
		return codeBlockCount;
	}

	public void incrementCodeBlockCount() {
		codeBlockCount++;
	}

	private int mainMethodCallCount = 0;

	public int getMainMethodCalls() {
		return mainMethodCallCount;
	}

	public void incrementCurrentMethodCallCount() {
		currentMethodCallCount++;
	}

	public void invalidateCurrentMethodCallCount() {
		currentMethodCallCount = 0;
	}

	public void incrementMainMethodCallCount() {
		mainMethodCallCount += currentMethodCallCount;
		currentMethodCallCount = 0;
	}

	private int formalArgumentsCount = 0;

	public int getFormalArgumentsCount() {
		return formalArgumentsCount;
	}

	public void incrementFormalArgumentsCount() {
		formalArgumentsCount++;
	}

	private int classDefinitionCount = 0;

	public int getClassDefinitionCount() {
		return classDefinitionCount;
	}

	public void incrementClassDefinitionCount() {
		classDefinitionCount++;
	}

	private int classVariableCount = 0;

	public int getClassVariableCount() {
		return classVariableCount;
	}

	public void incrementClassVariableCount() {
		classVariableCount ++;
	}

	void printSyntaxErrorDescription(String message) {
		parser.getLogger().info("Description: " + message);
	}

	//Semantic analysis

	private static final String THIS = "this";
	private static final String BRACKETS = "[]";

	private int variablePosition = 0;
	
	private int formalParameterCount = 0;
	private Obj currentMethod = null;

	private Struct currentClass = null;
	private int classVariablePosition = 0;

:}


terminal PROGRAM, LEFT_BRACE, RIGHT_BRACE, CONST, SEMICOLON, EQUALS, COMA, LEFT_BRACKET, RIGHT_BRACKET;
terminal CLASS, LEFT_PARENTHESIS, RIGHT_PARENTHESIS, STATIC, VOID, IF, ELSE, BREAK, CONTINUE, RETURN, FOR, INCREMENT;
terminal DECREMENT, OR, AND, NEW, DOT, EQUAL, NOT_EQUAL, GREATER_THAN, GREATER_OR_EQUAL_THAN, LESS_THAN;
terminal LESS_OR_EQUAL_THAN, PLUS, MINUS, PLUS_EQUALS, MINUS_EQUALS, TIMES, SOLIDUS, PERCENT, TIMES_EQUALS;
terminal SOLIDUS_EQUALS, PERCENT_EQUALS, READ, PRINT, EXTENDS;

terminal String IDENTIFIER, CHARACTER_CONSTANT;

terminal Integer NUMERICAL_CONSTANT;

terminal Boolean BOOLEAN_CONSTANT;

nonterminal Program, NonMethodDeclarationList, MethodDeclarationList;

nonterminal NonMethodDeclaration, ConstantDeclaration, GlobalVariableDeclaration, ClassDeclaration;

nonterminal ClassVariableDeclarationList, ClassVariableDeclaration, ClassMethodList;

nonterminal MethodDeclaration, FormalParameterList, MethodVariableDeclarationList, MethodVariableDeclaration,
			FormalParameter;

nonterminal StatementList, Statement, MatchedStatement, UnmatchedStatement, ReturnValue, PrintParameters;

nonterminal IfHeader, ForHeader;

nonterminal ForInitialStatement, ForCondition, ForPostStatement;

nonterminal DesignatorStatement, RestOfTheDesignatorStatement, MethodCall, ActualParameters, ExpressionList;

nonterminal Condition, ConditionTerm, ConditionFact, RestOfTheConditionFact, RelationalOperator;

nonterminal Expression, ExpressionPrefix, Term, Factor;

nonterminal OptionalParameters, ArraySize, ArrayIndexer;

nonterminal AssignOperation, AssignOperator;

nonterminal /*AdditionOperator,*/ AdditionOperatorLeft, AdditionOperatorRight;

nonterminal /*MultiplicationOperator,*/ MultiplicationOperatorLeft, MultiplicationOperatorRight;

//Semantic analysis non terminals
nonterminal Obj ProgramName;

nonterminal Struct Type, ParentClassDeclaration;

//constants
nonterminal Value Value;
nonterminal Constant Constant;
nonterminal List<Constant> ConstantList;

//varaibles
nonterminal Variable Variable;
nonterminal Boolean Brackets;

//global varaibles
nonterminal List<Variable> GlobalVariableList, ClassVariableList;

//method variables
nonterminal MethodTypeAndName;
nonterminal Struct ReturnType;
nonterminal List<Variable> MethodVariableList;

//class declaration
nonterminal ClassHeader;
nonterminal Boolean StaticModifier;

//Use of declaratiosn
nonterminal Obj Designator;

//For left associated and right associated operators, this super rule to sub rule passing
nonterminal LeftAssociatedTermList, RightAssociatedTermList, LeftAssociatedFactorList, RightAssociatedFactorList; 

Program ::= PROGRAM ProgramName:programObject 
			NonMethodDeclarationList 
			LEFT_BRACE 	
				MethodDeclarationList 
			RIGHT_BRACE 
			{:
				if (SymbolTable.currentScope.findSymbol(MAIN) == null) {
					parser.report_semantic_error("No main symbol found", programObjectleft);
				} 
				SymbolTable.chainLocalSymbols(programObject);
				SymbolTable.closeScope();
			:};

ProgramName ::= IDENTIFIER:name 
				{:
					RESULT  = SymbolTable.insert(Obj.Prog, name, SymbolTable.noType);
					SymbolTable.openScope();
				:}
				;

NonMethodDeclarationList ::= NonMethodDeclarationList NonMethodDeclaration
							 |
							 /* epsilon */
							 ;

NonMethodDeclaration ::= 	ConstantDeclaration
						 	|
						 	GlobalVariableDeclaration {: incrementGlobalArrayCount(); :}
						 	|
						 	ClassDeclaration
						 	;
			
ConstantDeclaration ::=	CONST Type:typeNode ConstantList:constants SEMICOLON
						{:
							if (typeNode != null && constants != null && typeNode != SymbolTable.noType) {
								for(Constant constant : constants) {
									if (!typeNode.assignableTo(constant.getValue().getType())) {
										parser.report_semantic_error("Value " + constant.getValue() + " is not of type " + SymbolTable.getBasicTypeName(typeNode), typeNodeleft);
									} else if (SymbolTable.currentScope.findSymbol(constant.getName()) != null) {
												parser.report_semantic_error("Redefinition of name " + constant.getName(), typeNodeleft);		
									} else {
										Obj newObject = SymbolTable.insert(Obj.Con, constant.getName(), typeNode);
										if (newObject != null && newObject != SymbolTable.noObj) {
											newObject.setAdr(constant.getValue().getValue());
										} else {
											parser.report_semantic_error("Could not insert symbol: " + constant.getName() + " into symbol table", typeNodeleft);
										}
									}
								}
							}
						:}
						;

ConstantList ::=	ConstantList:constants COMA Constant:constant 
					{: 
						incrementConstantCount(); 

						//Semantic analysis
						constants.add(constant);
						RESULT = constants;
					:}
				 	|
				 	Constant:constant 
				 	{: 
				 		incrementConstantCount();

				 		//Semantic analysis
				 		List<Constant> constants = new ArrayList<Constant>();
				 		constants.add(constant);
				 		RESULT = constants;
				 	:}
				 	;	

Constant ::=	IDENTIFIER:name EQUALS Value:value
				{:
					RESULT = new Constant(name, value);
				:};

Value ::=	NUMERICAL_CONSTANT:value
		  	{:
		  		RESULT = new Value(SymbolTable.intType, value.intValue());
		  	:}
		  	|
		  	CHARACTER_CONSTANT:value
		  	{:
		  		RESULT = new Value(SymbolTable.charType, (int)value.charAt(0));
		  	:}
		  	|
		  	BOOLEAN_CONSTANT:value
		  	{:
		  		RESULT = new Value(SymbolTable.boolType, value.booleanValue() ? 1 : 0);
		  	:}
		  	;

GlobalVariableDeclaration ::=	Type:typeNode GlobalVariableList:globalVariables SEMICOLON
								{:
									//Semantic analysis
									if (typeNode != null && globalVariables != null && typeNode != SymbolTable.noType) {
										for(Variable variable : globalVariables) {
											if(SymbolTable.currentScope.findSymbol(variable.getName()) != null) {
												parser.report_semantic_error("Redefinition of name " + variable.getName(), typeNodeleft);
											} else {
												Obj newObject = null;
												if (variable.isArray()) {
													newObject = SymbolTable.insert(Obj.Var, variable.getName(), new MyStruct(MyStruct.Array, typeNode));
												} else {
													newObject = SymbolTable.insert(Obj.Var, variable.getName(), typeNode);
												}
												if (newObject != null && newObject != SymbolTable.noObj) {
													newObject.setAdr(variablePosition);
													newObject.setLevel(SymbolTable.getCurrentLevel());
													variablePosition++;
												} else {
													parser.report_semantic_error("Could not insert symbol: " + variable.getName() + " into symbol table", typeNodeleft);
												}
											}
										}
									}
								:}
								|
								error SEMICOLON {: printSyntaxErrorDescription("Incorrect global variable list declaration"); :}
								|
								error COMA GlobalVariableList SEMICOLON {: printSyntaxErrorDescription("Incorrect global variable declaration"); :}
								;
								
GlobalVariableList ::=	GlobalVariableList:globalVariables COMA Variable:variable
						{: 
							incrementGlobalVariableCount(); 

							//Semantic analysis
							globalVariables.add(variable);
							RESULT = globalVariables;
						:}
				 		|
				 		Variable:variable
				 		{: 
				 			incrementGlobalVariableCount(); 

				 			//Semantic analysis
				 			RESULT = new ArrayList<Variable>();
				 			RESULT.add(variable);
				 		:}
				 		|
				 		GlobalVariableList COMA error {: printSyntaxErrorDescription("Incorrect global variable declaration"); :}
				 		|
				 		error {: printSyntaxErrorDescription("Incorrect global variable declaration"); :}
				 		;
				 
Variable ::=	IDENTIFIER:name Brackets:isArray
				{:
					RESULT = new Variable(name, isArray);
				:};

Brackets ::=	LEFT_BRACKET RIGHT_BRACKET
			 	{:
					incrementCurrentArrayCount();
					RESULT = true;
				:}
			 	|
			 	/* epsilon */
			 	{:
			 		RESULT = false;
			 	:}
			 	;
			 
ClassDeclaration ::=	ClassHeader
						LEFT_BRACE
							ClassVariableDeclarationList
							ClassMethodList
						 RIGHT_BRACE
						 {:
						 	incrementNonStaticMethodCount();
						 	incrementClassDefinitionCount();

						 	//Semantic analysis
						 	if (currentClass != null) {
						 		SymbolTable.chainLocalSymbols(currentClass);
						 		SymbolTable.closeScope();
						 	}

						 	classVariablePosition = 0;
						 	currentClass = null;
						 :};
				
ClassHeader ::= CLASS IDENTIFIER:name ParentClassDeclaration:typeNode
				{:
					if (SymbolTable.find(name) != SymbolTable.noObj) {
						parser.report_semantic_error("Redefinition of " + name, nameleft);
					} else {
						if (typeNode != null && typeNode != SymbolTable.noType) {
							currentClass = new MyStruct(MyStruct.Class, typeNode);
						} else {
							currentClass = new MyStruct(MyStruct.Class);
						}

						Obj newObject = SymbolTable.insert(Obj.Type, name, currentClass);
						if (newObject == null || newObject.equals(SymbolTable.noObj)) {
							parser.report_semantic_error("Could not insert name " + name + " into symbol table", nameleft);
						} else {
							SymbolTable.openScope();
							classVariablePosition = 0;
						}
					}
				:};


ParentClassDeclaration ::=	EXTENDS Type:typeNode
						   	{:
						   		//Semantic analysis
						   		if (typeNode != SymbolTable.noType) {
						   			if (typeNode.getKind() != Struct.Class) {
						   				parser.report_semantic_error("Name " + SymbolTable.getBasicTypeName(typeNode) + " is not a class type", typeNodeleft);
						   				
						   				RESULT = SymbolTable.noType;
						   			} else {
						   				RESULT = typeNode;
						   			}
						   		}
						   	:}
						   	|
						   	/* epsilon */
						   	{:
						   		RESULT = SymbolTable.noType;
						   	:}
						   	|
						   	error {: printSyntaxErrorDescription("Incorrect declaration of parent class"); :}
						   	;
					 
ClassVariableDeclarationList ::=	ClassVariableDeclarationList ClassVariableDeclaration
					  				|
					  				/* epsilon */
					  				;

ClassVariableDeclaration ::=	Type:typeNode ClassVariableList:variables SEMICOLON 
								{:
									//Semantic analysis
									if (typeNode != null && variables != null && typeNode != SymbolTable.noType) {
										for(Variable variable : variables) {
											if(SymbolTable.currentScope.findSymbol(variable.getName()) != null) {
												parser.report_semantic_error("Redefinition of name " + variable.getName(), typeNodeleft);
											} else {
												Obj newObject = null;
												if (variable.isArray()) {
													newObject = SymbolTable.insert(Obj.Fld, variable.getName(), new MyStruct(MyStruct.Array, typeNode));
												} else {
													newObject = SymbolTable.insert(Obj.Fld, variable.getName(), typeNode);
												}
												if (newObject != null && newObject != SymbolTable.noObj) {
													newObject.setAdr(classVariablePosition);
													newObject.setLevel(SymbolTable.getCurrentLevel());
													classVariablePosition++;
												} else {
													parser.report_semantic_error("Could not insert symbol: " + variable.getName() + " into symbol table", typeNodeleft);
												}
											}
										}
									}
								:}
								|
								error SEMICOLON {: printSyntaxErrorDescription("Incorrect class variable declaration"); :}
								|
								error {: printSyntaxErrorDescription("Incorrect class variable declaration"); :}
				  				;

ClassVariableList ::= 	ClassVariableList:variables COMA Variable:variable 
						{: 
							incrementClassVariableCount(); 

							//Semantic analysis
							variables.add(variable);
							RESULT = variables;
						:}
						|
						Variable:variable 
						{: 
							incrementClassVariableCount(); 

							//Semantic analysis
							RESULT = new ArrayList<Variable>();
							RESULT.add(variable);
						:}
						;

ClassMethodList ::= LEFT_BRACE
						MethodDeclarationList
					RIGHT_BRACE
					|
					/* epsilon */
					;

MethodDeclarationList ::= MethodDeclarationList MethodDeclaration
						  |
						  /* epsilon */
						  ;
						  
MethodDeclaration ::=	MethodTypeAndName LEFT_PARENTHESIS FormalParameterList RIGHT_PARENTHESIS
							MethodVariableDeclarationList
					  	LEFT_BRACE
					  		StatementList
					  	RIGHT_BRACE
					  	{:
							if (isMain(currentMethod.getName())) {
								incrementMainVariableCount();
								incrementMainMethodCallCount();
							} else {
								invalidateCurrentMethodVariableCount();
								invalidateCurrentArrayCount();
								invalidateCurrentMethodCallCount();
							}
							incrementCodeBlockCount();

							//Semantic analysis
							if (currentMethod != null) {
								SymbolTable.chainLocalSymbols(currentMethod);
								SymbolTable.closeScope();

								currentMethod.setLevel(formalParameterCount);
							}

							currentMethod = null;
							variablePosition = 0;
							formalParameterCount = 0;
						:}
						;
					  
MethodTypeAndName ::=	StaticModifier:isStatic ReturnType:typeNode IDENTIFIER:identifier
						{:
							//Semantic analysis
							if (typeNode != SymbolTable.noType) {
								if (SymbolTable.currentScope.findSymbol(identifier) != null) {
									parser.report_semantic_error("Redefinition of " + identifier, identifierleft);
								} else {
									if (currentClass == null && isStatic.booleanValue() == false) {
										currentMethod = SymbolTable.insert(MyObj.Global, identifier, typeNode);
									} else {
										if (isStatic != null && isStatic.booleanValue()) {
											currentMethod = SymbolTable.insert(MyObj.Static, identifier, typeNode);
										} else {
											currentMethod = SymbolTable.insert(Obj.Meth, identifier, typeNode);
										}
									}
									
									if (currentMethod == SymbolTable.noObj) {
										currentMethod = null;
										parser.report_semantic_error("Could not insert symbol " + identifier + "into symbol talbe", identifierleft);
									} else {
										variablePosition = 0;
										formalParameterCount = 0;

										SymbolTable.openScope();

										//If non-static method add this formal parameter
										if (currentClass != null && !isStatic.booleanValue()) {
											Obj newObject = SymbolTable.insert(MyObj.Var, THIS, currentClass);

											if (newObject == SymbolTable.noObj) {
												parser.report_semantic_error("Could not insert symbol " + THIS + " into symbol table", identifierleft);
											} else {
												newObject.setLevel(SymbolTable.getCurrentLevel());
												newObject.setFpPos(variablePosition);
												newObject.setAdr(variablePosition);
												variablePosition++;
												formalParameterCount++;
											}
										}
									}
								}
							}
						:}
						;

StaticModifier ::= 	STATIC:attribute
					{:
						incrementStaticMethodCount();

						//Semantic analysis
						if (currentClass == null) {
							parser.report_semantic_error("Static method outside class", attributeleft);
							RESULT = false;
						} else {
							RESULT = true;
						}
						
					:} 
				   	|
				   	/* epsilon */
				   	{:
				   		incrementCurrentNonStaticMethodCount();
				   		
				   		//Semantic analysis
						RESULT = false;
				   	:}
				   	;
				   	
ReturnType ::=	Type:typeNode
			   	{:
			   		RESULT = typeNode;
			   	:}
			   	|
			   	VOID
			   	{:
			   		RESULT = SymbolTable.voidType;
			   	:}
			   	;

FormalParameterList ::= FormalParameterList COMA FormalParameter
						|
						FormalParameter
						|
						/* epsilon */
						|
						FormalParameterList COMA error {: printSyntaxErrorDescription("Incorrect formal parameter declaration"); :}
						|
						error {: printSyntaxErrorDescription("Incorrect formal parameter declaration"); :}
						;	
						
FormalParameter ::= Type:typeNode IDENTIFIER:name Brackets:isArray
					{:
						incrementFormalArgumentsCount();
						
						//Semantic analysis
						//If the exists in the current scop then it is an error
						if (typeNode != null && typeNode != SymbolTable.noType) {
							if (SymbolTable.currentScope.findSymbol(name) != null) {
								parser.report_semantic_error("Redefinition of " + name, nameleft);
							} else {
								Obj newObject = null;
								if (isArray) {
									newObject = SymbolTable.insert(Obj.Var, name, new MyStruct(Struct.Array, typeNode));
								} else {
									newObject = SymbolTable.insert(Obj.Var, name, typeNode);
								}

								if (newObject == SymbolTable.noObj) {
									parser.report_semantic_error("Could not insert symbol " + name + " into symbol table", nameleft);
								} else {
									newObject.setLevel(SymbolTable.getCurrentLevel());
									newObject.setFpPos(variablePosition);
									newObject.setAdr(variablePosition);
									variablePosition++;
									formalParameterCount++;
								}
							}
						}
					:}
					;

MethodVariableDeclarationList ::= MethodVariableDeclarationList MethodVariableDeclaration
								  |
								  error {: printSyntaxErrorDescription("Incorrect local variable declaration"); :}
								  |
								  /* epsilon */
								  ;

MethodVariableDeclaration ::=	Type:typeNode MethodVariableList:locals SEMICOLON
								{:
									//Semantic analysis
									if (typeNode != null && locals != null && typeNode != SymbolTable.noType) {
										for(Variable localVariable : locals) {
											if (SymbolTable.currentScope.findSymbol(localVariable.getName()) != null) {
												parser.report_semantic_error("Redefinition of name " + localVariable.getName(), typeNodeleft);
											} else {
												Obj newObject = null;
												if (localVariable.isArray()) {
													newObject = SymbolTable.insert(Obj.Var, localVariable.getName(), new MyStruct(Struct.Array, typeNode));
												} else {
													newObject = SymbolTable.insert(Obj.Var, localVariable.getName(), typeNode);
												}

												if (newObject == SymbolTable.noObj) {
													parser.report_semantic_error("Could not insert symbol " + localVariable.getName() + " into symbol table", typeNodeleft);
												} else {
													newObject.setLevel(SymbolTable.getCurrentLevel());
													newObject.setFpPos(variablePosition);
													newObject.setAdr(variablePosition);
													variablePosition++;
												}
											}
										}
									}
								:}
								|
								Type error SEMICOLON {: printSyntaxErrorDescription("Incorrect local variable declaration"); :}
								;

MethodVariableList ::=	MethodVariableList:locals COMA Variable:variable 
						{: 
							incrementCurrentMethodVariableCount(); 

							//Semantic analysis
							locals.add(variable);
							RESULT = locals;
						:}
						|
						Variable:variable
						{: 
							incrementCurrentMethodVariableCount(); 

							//Semantic analysis
							RESULT = new ArrayList<Variable>();
							RESULT.add(variable);
						:}
						;

Type ::=	IDENTIFIER:name
			{:
				Obj typeNode = SymbolTable.find(name);
				if (SymbolTable.noObj.equals(typeNode)) {
					parser.report_semantic_error("no type named: " + name, nameleft);
					RESULT = SymbolTable.noType;
				} else if (Obj.Type != typeNode.getKind()) {
					parser.report_semantic_error("Name: " + name + " is not a type", nameleft);
					RESULT = SymbolTable.noType;
				} else {
					RESULT = typeNode.getType();
				}
			:}
			;

StatementList ::= StatementList Statement
				  |
				  /* epsilon */
				  ;

Statement ::= MatchedStatement
			  |
			  UnmatchedStatement
			  ;

IfHeader ::=	IF LEFT_PARENTHESIS Condition RIGHT_PARENTHESIS
				|
				IF LEFT_PARENTHESIS error RIGHT_PARENTHESIS {: printSyntaxErrorDescription("Incorrect condition declaration"); :}
				|
				IF LEFT_PARENTHESIS RIGHT_PARENTHESIS {: printSyntaxErrorDescription("Incorrect condition declaration"); :}
				;
			  
ForHeader ::=	FOR LEFT_PARENTHESIS ForInitialStatement SEMICOLON ForCondition SEMICOLON ForPostStatement RIGHT_PARENTHESIS
				|
				FOR LEFT_PARENTHESIS error RIGHT_PARENTHESIS {: printSyntaxErrorDescription("Incorrect for loop inner statements"); :}
				;

UnmatchedStatement ::= IfHeader 
							Statement
					   |
					   IfHeader
					   		MatchedStatement
					   ELSE
					   		UnmatchedStatement
					   | /* umatched for */
					   ForHeader
					 		UnmatchedStatement
					   ;
					 
MatchedStatement ::= 	IfHeader 
							MatchedStatement
						ELSE
							MatchedStatement
						| /* matched for */
						ForHeader
							MatchedStatement
						|
						DesignatorStatement SEMICOLON
						|
						BREAK SEMICOLON
						|
						CONTINUE SEMICOLON
					 	|
					 	RETURN ReturnValue SEMICOLON
					 	|
					 	READ LEFT_PARENTHESIS Designator RIGHT_PARENTHESIS SEMICOLON
					 	|
					 	PRINT LEFT_PARENTHESIS PrintParameters RIGHT_PARENTHESIS SEMICOLON
					 	|
					 	LEFT_BRACE
					 		StatementList
					 	RIGHT_BRACE
					 	{:
					 		incrementCodeBlockCount();
					 	:}			
					 	;

ReturnValue ::= Expression
				|
				/* epsilon */
				;
				
PrintParameters ::= Expression
					|
					Expression COMA NUMERICAL_CONSTANT
					;

ForInitialStatement ::= DesignatorStatement
						|
						error {: printSyntaxErrorDescription("Incorrect initial for loop statement"); :}
						|
						/* epsilon */
						;
						 
ForCondition ::=	Condition
				 	|
				 	error {: printSyntaxErrorDescription("Incorrect for condition statement"); :}
				 	|
				 	/* epsilon */
		  		 	;
		  		 
ForPostStatement ::= DesignatorStatement
					 |
					 error {: printSyntaxErrorDescription("Incorrect post for loop statement"); :}
					 |
					 /* epsilon */
					 ;

DesignatorStatement ::=	Designator RestOfTheDesignatorStatement;

RestOfTheDesignatorStatement ::= 	AssignOperation
									|
									MethodCall
								 	|
								 	INCREMENT
								 	|
								 	DECREMENT
								 	;

MethodCall ::=	LEFT_PARENTHESIS ActualParameters RIGHT_PARENTHESIS
			 	{:
			 		incrementCurrentMethodCallCount();
			 	:}
			 	;

AssignOperation ::= 	AssignOperator Expression
						|
						error {: printSyntaxErrorDescription("Incorrect asign operation syntax"); :}
						;
								 
ActualParameters ::= ExpressionList
					 |
					 /* epsilon */
					 |
					 error {: printSyntaxErrorDescription("Incorrect actual parameter list syntax"); :}
					 ;

ExpressionList ::=	ExpressionList COMA Expression
					|
					Expression
					;
				 				
Condition ::= Condition OR ConditionTerm
			  |
			  ConditionTerm
			  ;	
				 				
ConditionTerm ::=	ConditionTerm AND ConditionFact
				 	|
				 	ConditionFact
				 	;

ConditionFact ::= Expression RestOfTheConditionFact;

RestOfTheConditionFact ::= RelationalOperator Expression
						   |
						   /* epsilon */
						   ;
						   
Expression ::= ExpressionPrefix RightAssociatedTermList;

ExpressionPrefix ::= MINUS
					 |
					 /* epsilon */
					 ;

RightAssociatedTermList ::=	LeftAssociatedTermList AdditionOperatorRight RightAssociatedTermList
							|
							LeftAssociatedTermList
							;

LeftAssociatedTermList ::=	LeftAssociatedTermList AdditionOperatorLeft Term
							|
							Term
							;

Term ::=	RightAssociatedFactorList;

RightAssociatedFactorList ::=	LeftAssociatedFactorList MultiplicationOperatorRight RightAssociatedFactorList
								|
								LeftAssociatedFactorList
								;

LeftAssociatedFactorList ::=	LeftAssociatedFactorList MultiplicationOperatorLeft Factor
								|
								Factor
								;

Factor ::= Designator OptionalParameters
		   |
		   NUMERICAL_CONSTANT
		   |
		   CHARACTER_CONSTANT
		   |
		   BOOLEAN_CONSTANT
		   |
		   NEW Type ArraySize
		   |
		   LEFT_PARENTHESIS Expression RIGHT_PARENTHESIS
		   ;
		   
OptionalParameters ::= 	MethodCall
					   	|
					   	/* epsilon */
					   	;		   
		   						  
ArraySize ::= ArrayIndexer
			  |
			  /* epsilon */
			  ;
	   
ArrayIndexer ::=	LEFT_BRACKET Expression RIGHT_BRACKET
					|
					LEFT_BRACKET error RIGHT_BRACKET {: printSyntaxErrorDescription("Incorrect expression for array index"); :}
					;
					
					
Designator ::=	Designator:object DOT IDENTIFIER:name
				{:
					//Semantic analysis
					if (object != null) {
						if (object.getType().getKind() != MyStruct.Class) {
							parser.report_semantic_error("Symbol " + object.getName() + " is not of class type", objectleft);
							RESULT = null;
						} else {
							Obj member = null;
							//If object is THIS we need to search in top scope because members are not associated with class node yet
							if (THIS.equals(object.getName()) || (currentClass != null && object.getKind() == MyObj.Type && object.getType().equals(currentClass))) {
								member = SymbolTable.currentScope.getOuter().findSymbol(name);
							} else {
								SymbolDataStructure members = object.getType().getMembersTable();
								member = members.searchKey(name);
							}

							if (member == null) {
								parser.report_semantic_error("Symbol " + object.getName() + " has no member of name " + name, objectleft);
								RESULT = null;
							} else if(object.getKind() == MyObj.Type && member.getKind() != MyObj.Static) {
								parser.report_semantic_error("Use of non-static method or varaible through class name symbol " + object.getName(), objectleft);
								RESULT = null;
							} else {
								RESULT = member;
							}
						}
					} else {
						RESULT = null;
					}
				:}
				|
				Designator:object ArrayIndexer
				{:
					//Semantic analysis
					if (object != null) {
						if(object.getType().getKind() != MyStruct.Array) {
							parser.report_semantic_error("Symbol " + object.getName() + " is not an array", objectleft);
							RESULT = null;
						} else if(object.getType().getElemType() == null) {
							parser.report_semantic_error("Internal error, array without an element type", objectleft);
							RESULT = null;
						} else {
							//Return temp object that represents and array element
							RESULT = new MyObj(MyObj.Var, object.getName(), object.getType().getElemType());
						}
					} else {
						RESULT = null;
					}
				:}
				|
				IDENTIFIER:name 
				{:
					//Semantic analysis
					Obj object = SymbolTable.find(name);
					if (object.equals(SymbolTable.noObj)) {
						if (THIS.equals(name)) {
							parser.report_semantic_error("Use of " + THIS + " outside of class non-static method", nameleft);
							RESULT = null;
						} else {
							parser.report_error("Use of undefined symbol " + name, nameleft);
							RESULT = null;
						}
					} else {	
						RESULT = object;
					}
				:}
				;
			   
AssignOperator ::= 	EQUALS
					|
					AdditionOperatorRight
					|
					MultiplicationOperatorRight
					;

RelationalOperator ::= EQUAL
					   |
					   NOT_EQUAL
					   |
					   GREATER_THAN
					   |
					   GREATER_OR_EQUAL_THAN
					   |
					   LESS_THAN
					   |
					   LESS_OR_EQUAL_THAN
					   ;
					   
/*AdditionOperator ::= AdditionOperatorLeft
					 |
					 AdditionOperatorRight
					 ;					 */  
					 
AdditionOperatorLeft ::= PLUS
						 |
						 MINUS
						 ;					  
					   
AdditionOperatorRight ::= PLUS_EQUALS
						  |
						  MINUS_EQUALS
						  ;
						   
/*MultiplicationOperator ::= MultiplicationOperatorLeft
						   |
						   MultiplicationOperatorRight
						   ;*/
						
MultiplicationOperatorLeft ::= TIMES
							   |
							   SOLIDUS
							   |													   
							   PERCENT
							   ;
								
MultiplicationOperatorRight ::= TIMES_EQUALS
							    |
							    SOLIDUS_EQUALS
							    |
							    PERCENT_EQUALS
							    ;
						   
				 				
				 				
				 				
				 				
				 				
				 				